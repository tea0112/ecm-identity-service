plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.liquibase.gradle' version '2.2.2'
}

allprojects {
    group = 'com.ecm.security'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.6"
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
}

bootJar { enabled = false }
jar { enabled = false }

liquibase {
    activities {
        local {
            changelogFile 'db/changelog/db.changelog-master.xml'
            url System.getenv('DB_URL') ?: 'jdbc:postgresql://localhost:5432/ecm_identity_dev'
            username System.getenv('DB_USERNAME') ?: 'dev_ecm'
            password System.getenv('DB_PASSWORD') ?: 'dev_ecm!23456'
            driver 'org.postgresql.Driver'
            defaultSchemaName System.getenv('DB_SCHEMA') ?: 'dev_ecm'
        }
    }
}
